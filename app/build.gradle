apply plugin: 'com.android.application'
apply plugin: "jacoco"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "nl.jmuijsenberg.androiddemo"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
        }

        release {
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // See http://tools.android.com/tech-docs/new-build-system/user-guide
    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError false

        // if true, generate an XML report for use by for example Jenkins
        xmlReport true

        // required for butterknife see http://jakewharton.github.io/butterknife/
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

// https://docs.gradle.org/current/userguide/sonar_runner_plugin.html
sonarRunner {
    sonarProperties {
        // See http://docs.sonarqube.org/display/PLUG/Android+Lint+Plugin
        property "sonar.android.lint.report", "${buildDir}/outputs/lint-results.xml"
    }
}

project(":app") {
    sonarRunner {
        sonarProperties {
            property "sonar.language", "java"

            // https://gist.github.com/hal9002/004946a4ca10965648bc
            property "sonar.sources", project.android.sourceSets.main.java.srcDirs
            property "sonar.java.libraries", project.android.applicationVariants.toList().first().javaCompile.classpath
            property "sonar.java.binaries", "$project.buildDir/intermediates/classes/debug"

            property "sonar.tests", project.android.applicationVariants.testVariant.javaCompile.source[0]
            property "sonar.junit.reportsPath", "build/outputs/androidTest-results/connected"
            property 'sonar.jacoco.reportPath', "build/outputs/code-coverage/connected/coverage.ec"
        }
    }
}

// Test coverage with Jacoco on Android gradle project with injection frameworks like Butterknife
// see https://gist.github.com/josomers/9f90b017e321c54bb918
task jacocoTestReport(type: JacocoReport, dependsOn: ['test']) {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            exclude: [
              '**/R*.class',
              '**/BuildConfig*',
              '**/*$*.class',
              '**/*$*.java']
    )

    sourceDirectories = files(['src/main/java'])
    executionData = fileTree(dir: rootProject.rootDir, includes: ['**/*.exec', '**/*.ec'])
}

ext {
    supportLibrary = '22.2.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // ViewPager support see https://developer.android.com/tools/support-library/features.html
    compile "com.android.support:support-v4:${supportLibrary}"
    compile "com.android.support:appcompat-v7:${supportLibrary}"

    // Module dependencies
    compile project(':repository')
    compile project(':repositorysqlite')
    compile project(':utilandroid')
    compile project(':utiljava')
    compile project(':viewmodels')
    compile project(':devices')
    compile project(':devicesandroid')
    compile project(':control')

    // RxJava support
    compile 'io.reactivex:rxjava:1.0.12'

    // Facebook Stetho support see http://facebook.github.io/stetho/
    // Use chrome://inspect to inspect via chrome browser
    compile 'com.facebook.stetho:stetho:1.1.1'

    // ButterKnife view injection support see http://jakewharton.github.io/butterknife/
    compile 'com.jakewharton:butterknife:7.0.1'

    // LeakCanary suppor see https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
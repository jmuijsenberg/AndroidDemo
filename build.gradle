// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: "sonar-runner"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// Avoid sonarRunner fails when any unit test fails
// See https://discuss.gradle.org/t/sonarrunner-test-dependency/1876
tasks.sonarRunner.dependsOn = []

// https://docs.gradle.org/current/userguide/sonar_runner_plugin.html
sonarRunner {
    toolVersion = '2.3' // default

    sonarProperties {
        property "sonar.host.url", "http://localhost:9000/"
        property "sonar.jdbc.url", "jdbc:mysql://localhost/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", System.getenv('SONAR_USERNAME')
        property "sonar.jdbc.password", System.getenv('SONAR_PASSWORD')

        // See http://stackoverflow.com/questions/20117545/sonar-how-do-i-use-gradle-to-inspect-an-android-project
        property "sonar.projectKey", "AndroidDemo"
        property "sonar.projectName", "AndroidDemo"
        property "sonar.projectVersion", "V1.0"

        // See http://stackoverflow.com/questions/22174501/sonarqube-not-picking-up-unit-test-coverage
        property "sonar.junit.reportsPath", "build/test-reports"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "build/jacoco/test.exec"

        // See http://docs.sonarqube.org/display/PLUG/Build+Stability+Plugin
        property "sonar.build-stability.url", "Jenkins:http://localhost:8080/job/Build%20AndroidDemo/"
    }
}

// Ensure same library versions are used by all modules
ext {
    supportLibraryVersion = '23.0.1'
    rxjavaVersion = '1.0.14'
    rxandroidVersion = '0.23.0'
    stethoVersion = '1.1.1'
    butterknifeVersion = '7.0.1'
    leakcanaryVersion = '1.3.1'
    cucumberVersion = '1.2.2' // Latest versions 1.2.3/1.2.4 are not compatible see https://github.com/cucumber/cucumber-jvm-deps/pull/4
    androidTestVersion = '0.4.1'
    espressoVersion = '2.2.1'
    dexMakerVersion='1.2'
    mockitoVersion = '1.10.19'
    junitVersion = '4.12'
}

allprojects {
    repositories {
        jcenter()
    }
}
